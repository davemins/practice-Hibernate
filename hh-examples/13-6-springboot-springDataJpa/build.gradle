plugins {
  id 'java'
  id 'io.freefair.lombok'

  id 'org.springframework.boot'
  id 'io.spring.dependency-management'
}

// $ gradle bootRun -Pargs=firstArg
// $ gradle bootRun -Pargs=firstArg,secondArg
bootRun {
  if (project.hasProperty('args')) {
    args project.args.split(',')
  }
}

clean {
  delete "bin"
}

repositories {
  mavenCentral()
}

dependencies {
  implementation     'org.springframework.boot:spring-boot-starter-data-jpa'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'

  developmentOnly    'org.springframework.boot:spring-boot-devtools'

  runtimeOnly        'com.h2database:h2'
}

test {
  useJUnitPlatform()

  outputs.upToDateWhen {false}
  testLogging {
    events = ['passed', 'skipped', 'failed']
    showStandardStreams = true
    exceptionFormat = 'full'
  }
}

allprojects {
  task ctest(dependsOn: classes, type: JavaExec) {
    group = 'Application'
    description = 'Runs CreateTest'

    mainClass = 'com.oreilly.hh.TestRunner'
    args 'createTest'
    classpath = sourceSets.main.runtimeClasspath
  }

  task qtest(dependsOn: classes, type: JavaExec) {
    group = 'Application'
    description = 'Runs QueryTest'

    mainClass = 'com.oreilly.hh.TestRunner'
    args 'queryTest'
    classpath = sourceSets.main.runtimeClasspath
  }

  task atest(dependsOn: classes, type: JavaExec) {
    group = 'Application'
    description = 'Runs AlbumTest'

    mainClass = 'com.oreilly.hh.TestRunner'
    args 'albumTest'
    classpath = sourceSets.main.runtimeClasspath
  }
}

