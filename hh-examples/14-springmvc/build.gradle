plugins {
  id 'io.freefair.lombok'              apply false
  id 'com.bmuschko.cargo'              apply false

  id 'org.springframework.boot'        apply false
  id 'io.spring.dependency-management' apply false
}

allprojects {
  apply plugin: 'io.spring.dependency-management'
  dependencyManagement {
    imports {
      mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
  }
}

allprojects {
  apply plugin: 'war'
  apply plugin: 'io.freefair.lombok'

  repositories {
    mavenCentral()
  }

  dependencies {
    // Slf4j
    implementation     'org.slf4j:slf4j-api'
    runtimeOnly        'ch.qos.logback:logback-classic'

    // JUnit 5
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly    'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.assertj:assertj-core'

    // Spring
    implementation     'org.springframework:spring-context'
    implementation     'org.springframework:spring-orm'               // Hibernate, JPA, MyBatis
    implementation     'org.springframework:spring-webmvc'            // Spring MVC
    testImplementation 'org.springframework:spring-test'

    // Spring Data Jpa
    //implementation   'org.springframework.data:spring-data-jpa'

    // Hibernate
    implementation     'org.hibernate.orm:hibernate-core'
    runtimeOnly        'org.hibernate.orm:hibernate-hikaricp'

    // JDBC Driver
    runtimeOnly        'com.h2database:h2'
    runtimeOnly        'org.postgresql:postgresql'
  }
}

// Test
allprojects {
  test {
    useJUnitPlatform()

    outputs.upToDateWhen {false}
    testLogging {
      events = ['passed', 'skipped', 'failed']
      showStandardStreams = true
      exceptionFormat = 'full'
    }
  }
}

import org.apache.tools.ant.taskdefs.condition.Os

// Web Application
allprojects {
  apply plugin: 'war'                 // providedCompile, providedRuntime
  apply plugin: 'com.bmuschko.cargo'  // cargoRunLocal

  configurations {
    tomcatZip
  }

  cargo {
    containerId = 'tomcat9x'
    port = 8080

    deployable {
      context = 'springmvc'
    }

    local {
      // Automatically download and install Tomcat
      installer {
        installConfiguration = configurations.tomcatZip
        // FAMILY_UNIX, FAMILY_WINDOWS, FAMILY_MAC
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
          downloadDir = file('c:/tomcat')
          extractDir  = file('c:/tomcat')
        } else {
          downloadDir = file('/opt/javatool/tomcat')
          extractDir  = file('/opt/javatool/tomcat')
        }
      }
    }
  }

  cargoRunLocal {
    dependsOn war
  }

  def tomcatVersion = dependencyManagement.importedProperties['tomcat.version']

  dependencies {
    // Gradle Cargo Plugin
    cargo             'org.codehaus.cargo:cargo-core-uberjar:latest.release'
    cargo             'org.codehaus.cargo:cargo-ant:latest.release'
    cargo             'org.slf4j:slf4j-api'

    // Tomcat type=ZIP/tar.gz
    tomcatZip         "org.apache.tomcat:tomcat:${tomcatVersion}@tar.gz"

    // Tomcat Servlet/JSP/EL Library
    providedCompile   'org.apache.tomcat:tomcat-jsp-api'
    //providedCompile 'org.apache.tomcat:tomcat-servlet-api'
    //providedCompile 'org.apache.tomcat:tomcat-el-api'

    // Tomcat JSTL Library
    runtimeOnly       'org.apache.taglibs:taglibs-standard-spec:latest.release'
    runtimeOnly       'org.apache.taglibs:taglibs-standard-impl:latest.release'
  }
}

