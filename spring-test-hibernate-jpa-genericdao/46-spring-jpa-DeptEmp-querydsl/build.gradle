plugins {
  id 'io.freefair.lombok'

  id 'org.springframework.boot'        apply false
  id 'io.spring.dependency-management' apply false
}

allprojects {
  apply plugin: 'io.spring.dependency-management'
  dependencyManagement {
    imports {
      mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
  }

  apply plugin: 'idea'
  apply plugin: 'eclipse'

  idea {
    module.downloadJavadoc = true
  }

  eclipse {
    classpath.downloadJavadoc = true
  }
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'application'

  application {
    mainClass = project.findProperty('mainClass') ?: 'exercise.App'
  }

  tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:deprecation'  // @SuppressWarnings("deprecation")
    options.compilerArgs << '-Xlint:unchecked'    // @SuppressWarnings("unchecked")
    options.compilerArgs << '-parameters'         // Java 8+
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    // Slf4j
    implementation     'org.slf4j:slf4j-api'
    runtimeOnly        'ch.qos.logback:logback-classic'

    // JUnit 5
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core'

    // Hibernate
    implementation     'org.hibernate.orm:hibernate-core'
    implementation     'org.hibernate.orm:hibernate-ant'
    implementation     'org.hibernate.orm:hibernate-hikaricp'
    implementation     'com.zaxxer:HikariCP'                         // for Spring Configuration

    // JDBC Driver
    runtimeOnly        'com.h2database:h2'
    runtimeOnly        'org.postgresql:postgresql'
  }
}

// Spring
allprojects {
  dependencies {
    implementation     'org.springframework:spring-context'
    //implementation   'org.springframework:spring-context-support'  // EhCache, Freemarker, Quartz, JavaMail
    implementation     'org.springframework:spring-orm'              // Hibernate, JPA
    implementation     'org.springframework.data:spring-data-jpa'
    //implementation   'org.springframework:spring-jdbc'             // JdbcTemplate, MyBatis
    testImplementation 'org.springframework:spring-test'
  }
}

ext {
  querydslProjects  = allprojects.findAll { project -> project.name =~ /querydsl/  }
}

// QueryDSL
configure(querydslProjects) {
  dependencies {
    // QueryDSL 4
    // Classifier of com.querydsl:querydsl-apt: {general, hibernate, jdo, jpa, jakarta}
    // http://www.querydsl.com/static/querydsl/latest/reference/html/ch03s03.html

    //   QueryDSL JPA
    //implementation      'com.querydsl:querydsl-jpa'
    implementation      'com.querydsl:querydsl-jpa::jakarta'
    implementation      'jakarta.annotation:jakarta.annotation-api'
    //annotationProcessor 'com.querydsl:querydsl-apt::jpa'
    annotationProcessor 'com.querydsl:querydsl-apt::jakarta'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'

    // QueryDSL for Spring
    implementation      'com.querydsl:querydsl-sql-spring:latest.release'
  }
}

// SQL
allprojects {
  configurations {
    dbtool
  }

  dependencies {
    dbtool 'com.h2database:h2'
    dbtool 'org.hsqldb:hsqldb'
    dbtool 'org.postgresql:postgresql'
  }

  task dbinit(type: JavaExec) {
    group = 'Application'
    description = 'Initializes Database'

    mainClass = 'org.h2.tools.RunScript'
    args   '-url',        jdbcUrl
    args   '-user',       jdbcUsername
    args   '-password',   jdbcPassword
    args   '-script',     'setup.sql'       // default: backup.sql
    args   '-showResults'
    classpath = configurations.dbtool
  }

  task db(type: JavaExec) {
    group = 'Application'
    description = 'Runs DatabaseManagerSwing'

    mainClass = 'org.hsqldb.util.DatabaseManagerSwing'
    args   '--driver',    jdbcDriver
    args   '--url',       jdbcUrl
    args   '--user',      jdbcUsername
    if (jdbcPassword != '') {               // for Windows
      args '--password',  jdbcPassword
    }
    classpath = configurations.dbtool       // libraries only
  }

  task schema(dependsOn: classes, type: JavaExec) {
    group = 'Application'
    description = 'Generates the database schema from entities'

    mainClass = 'org.hibernate.orm.orm.tool.hbm2ddl.SchemaExport'
    args '--config=hibernate.cfg.xml', '--text', '--format', '--delimiter=;'
    //args '--create'
    classpath = sourceSets.main.runtimeClasspath
  }

  task schemajpa(dependsOn: classes, type: JavaExec) {
    group = 'Application'
    description = 'Generates the database schema from JPA entities'

    mainClass = 'exercise.GenerateJpaSchema'
    classpath = sourceSets.main.runtimeClasspath
  }

}

// Test
allprojects {
  test {
    useJUnitPlatform()

    outputs.upToDateWhen {false}
    testLogging {
      events = ['passed', 'skipped', 'failed']
      showStandardStreams = true
      exceptionFormat = 'full'
    }
  }
}

// Resource Filtering
allprojects {
  tasks.withType(Copy) {
    def props = project.ext.properties.findAll {
      it.key.startsWith('jdbc')      && (it.value instanceof String) ||
      it.key.startsWith('hibernate') && (it.value instanceof String) ||
      it.key.startsWith('jpa')       && (it.value instanceof String)
    }
    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: props)
  }

  task printProps {
    group = 'Application'
    description = 'Displays important properties'

    doLast {
      println "jdbcDriver       = ${jdbcDriver}"
      println "jdbcUrl          = ${jdbcUrl}"
      println "jdbcUsername     = ${jdbcUsername}"
      println "jdbcPassword     = ${jdbcPassword}"
      println "jdbcDataSource   = ${jdbcDataSource}"
      println "hibernateDialect = ${hibernateDialect}"
      println "mainClass        = ${application.mainClass.get()}"
    }
  }
}

