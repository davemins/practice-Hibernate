plugins {
  id 'java'
  id 'application'

  id 'org.springframework.boot'        version 'latest.release' apply false
  id 'io.spring.dependency-management' version 'latest.release' apply false
}

apply plugin: 'io.spring.dependency-management'
dependencyManagement {
  imports {
    mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << '-Xlint:deprecation'  // @SuppressWarnings("deprecation")
  options.compilerArgs << '-Xlint:unchecked'    // @SuppressWarnings("unchecked")
  options.compilerArgs << '-parameters'
}

application {
  mainClass = 'com.oreilly.hh.App'
}

repositories {
  mavenCentral()
}

dependencies {
  // Slf4j
  implementation     'org.slf4j:slf4j-api'
  runtimeOnly        'ch.qos.logback:logback-classic'

  // JUnit 5
  testImplementation 'org.junit.jupiter:junit-jupiter'
  testImplementation 'org.assertj:assertj-core'
  testImplementation 'org.hamcrest:hamcrest-library'
}

test {
  // Enable JUnit 5 (Gradle 4.6+)
  useJUnitPlatform()

  outputs.upToDateWhen {false}
  testLogging {
    events = ['passed', 'skipped', 'failed']
    showStandardStreams = true
    exceptionFormat = 'full'
  }
}

task helloworld(dependsOn: classes, type: JavaExec) {
  group = 'Application'
  description = 'Runs HelloWorld'

  mainClass = 'com.oreilly.hh.HelloWorld'
  classpath = sourceSets.main.runtimeClasspath
}

